apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.cabbage.boreas"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // launchctl setenv ANDROID_KEYSTORE_PATH /Users/Leo/Documents/KeyStore/
    def isBuddyBuild = System.getenv('IS_BUDDY_BUILD')
    if (isBuddyBuild == null) {
        def keyStorePath = System.getenv('ANDROID_KEYSTORE_PATH')
        keyStorePath = keyStorePath == null ? 'C:/KeyStore/' : keyStorePath

        File propsFile = file(keyStorePath + 'keyStoreProperties.txt')
        def props = new Properties()
        props.load(new FileInputStream(propsFile))

        def debugConfigName = 'debug'
        def releaseConfigName = 'release'

        signingConfigs {
            debug {
                storeFile = file(keyStorePath + props[debugConfigName + 'StoreFile'])
                storePassword = props[debugConfigName + 'StorePassword']
                keyAlias = props[debugConfigName + 'KeyAlias']
                keyPassword = props[debugConfigName + 'KeyPassword']
            }

            release {
                storeFile = file(keyStorePath + props[releaseConfigName + 'StoreFile'])
                storePassword = props[releaseConfigName + 'StorePassword']
                keyAlias = props[releaseConfigName + 'KeyAlias']
                keyPassword = props[releaseConfigName + 'KeyPassword']

            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

final SUPPORT_LIBRARY_VERSION = "25.0.0"
final DAGGER_VERSION = "2.6"
final RETROFIT_VERSION = "2.1.0"
final FIREBASE_VERSION = "9.8.0"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    // Dagger 2 and Compiler
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    // Needed for @Generated annotation (missing in Java <= 1.6; therefore, Android)
    provided 'org.glassfish:javax.annotation:10.0-b28'
    // ReactiveX
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    // Retrofit
    compile("com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}") {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }

    compile "com.google.guava:guava:18.0"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'

    // Firebase
    compile "com.google.firebase:firebase-core:${FIREBASE_VERSION}"
    compile "com.google.firebase:firebase-database:${FIREBASE_VERSION}"
    compile "com.google.firebase:firebase-crash:${FIREBASE_VERSION}"
    compile 'com.google.android.gms:play-services-auth:9.8.0'

    compile 'com.jakewharton:butterknife:8.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.facebook.stetho:stetho:1.3.1'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'